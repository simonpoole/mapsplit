

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.breadmoirai:github-release:2.2.11"
    }
}

plugins {
    id "org.sonarqube" version "2.7"
}

apply plugin: "com.github.breadmoirai.github-release"
apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: 'eclipse'

version = '0.4.0'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'http://nexus.onebusaway.org/nexus/content/repositories/public/' }
    maven { url 'https://dl.bintray.com/content/simonpoole/div' }
    //    flatDir {
    //        dirs 'lib'
    //    }
}

ext {
    github_token = System.getenv('GITHUB_TOKEN')
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'mapsplit fat jar',
        'Implementation-Version': "${project.version}",
        'Main-Class': 'dev.osm.mapsplit.MapSplit'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
fatJar.group = 'build'

sonarqube {
    properties {
        property "sonar.sourceEncoding","UTF-8"
        property "sonar.java.binaries", "build/classes/java/main/"
        property "sonar.jacoco.reportPaths","build/jacoco/test.exec"
    }
}

test{
    maxHeapSize = "12G"
}

dependencies {
    compile 'org.openstreetmap.osmosis:osmosis-core:0.47'
    compile 'org.openstreetmap.osmosis:osmosis-pbf:0.47'
    compile 'com.google.protobuf:protobuf-java:3.7.1'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.imintel:mbtiles4j:1.1.0'
    compile 'org.jetbrains:annotations:15.0'
    compile 'com.zaxxer:SparseBitSet:1.1'
    compile 'org.xerial.larray:larray_2.12:0.4.0'
    testCompile 'junit:junit:4.12'
}

githubRelease {
    token "${github_token}"
    owner "simonpoole"
    tagName "${version}"
    releaseName "${version}"
    body ""
    FilenameFilter filter = { dir, filename -> filename.contains(version) }
    releaseAssets = jar.destinationDir.listFiles filter
    overwrite true
}
